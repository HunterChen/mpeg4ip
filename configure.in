dnl Process this file with autoconf to produce a configure script.
AC_INIT()
AC_CONFIG_AUX_DIR(config)

AM_PROG_LIBTOOL
AM_INIT_AUTOMAKE(mpeg4ip, 0.7)

dnl Detect the canonical host and target build environment
AC_CANONICAL_HOST
AC_CANONICAL_TARGET

dnl Checks for programs.
AC_PROG_CC
AC_PROG_CXX
AC_PROG_LIBTOOL

dnl Checks for libraries.

dnl Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h unistd.h)

dnl Checks for typedefs, structures, and compiler characteristics.

dnl Checks for library functions.
AC_CHECK_FUNCS(strerror)

dnl Allow server build to be disabled
AC_ARG_ENABLE(server, 
[  --enable-server        enable server build],
[ case "${enableval}" in
  yes) server=true ;;
  no)  server=false ;;
  *)   AC_MSG_ERROR(bad value ${enableval} for --enable-server) ;;
esac],
[server=true])
AM_CONDITIONAL(SERVER, test $server = true)

dnl Allow player build to be disabled
AC_ARG_ENABLE(player, 
[  --enable-player        enable player build],
[ case "${enableval}" in
  yes) player=true ;;
  no)  player=false ;;
  *)   AC_MSG_ERROR(bad value ${enableval} for --enable-player) ;;
esac],
[player=true])
AM_CONDITIONAL(PLAYER, test $player = true)

dnl ### Checking for the existence of files
dnl MY_CHECK_FILE(FILE, [ACTION-IF-FOUND [, ACTION-IF-NOT-FOUND]])

AC_DEFUN(MY_CHECK_FILE,
[
dnl Do the transliteration at runtime so arg 1 can be a shell variable.
ac_safe=`echo "$1" | sed 'y%./+-%__p_%'`
AC_MSG_CHECKING([for $1])
AC_CACHE_VAL(ac_cv_file_$ac_safe,
  if test -r $1; then
    eval "ac_cv_file_$ac_safe=yes"
  else
    eval "ac_cv_file_$ac_safe=no"
  fi
)dnl
if eval "test \"`echo '$ac_cv_file_'$ac_safe`\" = yes"; then
  AC_MSG_RESULT(yes)
  ifelse([$2], , :, [$2])
else
  AC_MSG_RESULT(no)
ifelse([$3], , , [$3])
fi
])

MY_CHECK_FILE(/usr/include/glib-1.2/glib.h,
	GLIB_INCLUDES="-I/usr/lib/glib/include -I/usr/include/glib-1.2",
	GLIB_INCLUDES="-I/usr/lib/glib/include")
AC_SUBST(GLIB_INCLUDES)

MY_CHECK_FILE(/usr/include/gtk-1.2/gtk/gtk.h,
	GTK_INCLUDES="-I/usr/include/gtk-1.2",
	GTK_INCLUDES="-I/usr/include/gtk")
AC_SUBST(GTK_INCLUDES)

    AC_ARG_ENABLE(mmx,
[  --enable-mmx           use MMX assembly on x86 [default=yes]],
                  , enable_mmx=yes)

    if test x$enable_mmx = xyes; then
        AC_PATH_PROG(NASM, nasm)
        if test x$NASM = x -o x$NASM = x'"$NASM"'; then
            : # nasm isn't installed
			AM_CONDITIONAL(USE_MMX, false)
        else
			case $target in
			  i?86*)
				CFLAGS="$CFLAGS -DUSE_MMX"
				CXXFLAGS="$CXXFLAGS -DUSE_MMX"
				case $ARCH in
				  win32)
					  NASMFLAGS="-f win32"
					  ;;
				  *)
					  NASMFLAGS="-f elf"
					  ;;
				esac
				AC_SUBST(NASMFLAGS)
				AM_CONDITIONAL(USE_MMX, true)
				;;
			  *)
				AM_CONDITIONAL(USE_MMX, false)
				;;
			esac
        fi
	else
		AM_CONDITIONAL(USE_MMX, false)
    fi

dnl Now output the Makefile's
AC_OUTPUT( \
	Makefile \
	lib/Makefile \
	lib/avi/Makefile \
	lib/bitstream/Makefile \
	common/Makefile \
	common/lib/Makefile \
	common/lib/config_file/Makefile \
	common/lib/msg_queue/Makefile \
	common/mp4/Makefile \
	common/video/Makefile \
	server/Makefile \
	server/audio/Makefile \
	server/audio/faac/Makefile \
	server/video/Makefile \
	server/video/divx/Makefile \
	server/video/ffmpeg/Makefile \
	server/lib/Makefile \
	server/live/Makefile \
	server/live/gui/Makefile \
	server/util/Makefile \
	server/util/mp4encode/Makefile \
	server/util/avi2raw/Makefile \
	server/util/rgb2yuv/Makefile \
	server/util/lboxcrop/Makefile \
	server/util/mp4apkt/Makefile \
	server/util/mp4vpkt/Makefile \
	server/util/mp4vhdr/Makefile \
	server/util/mp4dump/Makefile \
	server/util/mp4extract/Makefile \
	player/Makefile \
	player/lib/Makefile \
	player/lib/sdp/Makefile \
	player/lib/rtsp/Makefile \
	player/lib/audio/Makefile \
	player/lib/audio/faad/Makefile \
	player/lib/audio/mp3/Makefile \
	player/lib/video/Makefile \
	player/lib/video/divx/Makefile \
	player/src/Makefile \
	player/src/codec/Makefile \
	player/src/codec/aa/Makefile \
	player/src/codec/divx/Makefile \
	player/src/codec/mp3/Makefile \
	player/src/codec/mpeg4/Makefile \
	player/src/codec/wav/Makefile \
	util/Makefile \
	util/yuv/Makefile \
)
